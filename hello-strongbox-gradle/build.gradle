apply plugin: 'java'
apply plugin: 'maven'

compileJava {
    targetCompatibility = "1.8"
    sourceCompatibility = "1.8"
    options.encoding = 'UTF-8'
}

group = 'org.carlspring.strongbox.examples'
version = '1.0'

description = "Example project that shows how to use gradle to build and deploy artifacts to Strongbox"

configurations {
    deployerJars
}

repositories {
    maven { url "http://dev.carlspring.org/nexus/content/groups/carlspring" }
    maven { url "http://localhost:48080/storages/storage0/releases/" }
    maven { url "http://localhost:48080/storages/storage0/snapshots/" }
    maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    deployerJars "org.apache.maven.wagon:wagon-http:2.2"
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

// Compare to maven deploy
// Gradle build Configuration to publish artifacts to maven artifact manager
uploadArchives {
    repositories.mavenDeployer {
            configuration = configurations.deployerJars
            repository(url: "http://localhost:48080/storages/storage0/releases/") {
                authentication(userName: "maven", password: "password")
            }
            pom.version = version
            pom.artifactId = "hello-strongbox-gradle"
            pom.groupId = group
    }
}

// ------------------------------------------------------
// EXAMPLE: custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar
    archives javadocJar
}

// EXAMPLE: make artifact deployment a part of build everytime
// comment or remove if you don't need that
build.dependsOn uploadArchives